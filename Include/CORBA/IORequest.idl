/// \file
/*
* Nirvana IDL support library.
*
* This is a part of the Nirvana project.
*
* Author: Igor Popov
*
* Copyright (c) 2021 Igor Popov.
*
* This program is free software; you can redistribute it and/or modify
* it under the terms of the GNU Lesser General Public License as published by
* the Free Software Foundation; either version 3 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU Lesser General Public
* License along with this library.  If not, see <http://www.gnu.org/licenses/>.
*
* Send comments and/or bug reports to:
*  popov.nirvana@gmail.com
*/
#ifndef CORBA_IOREQUEST_IDL_
#define CORBA_IOREQUEST_IDL_

module Nirvana {

native Pointer;
native ConstPointer;
native Size;

};

module CORBA {

typeprefix CORBA "omg.org";

pseudo interface TypeCode;

module Internal {

typeprefix CORBA::Internal "";

native _Interface;
native CharPtr;
native ConstCharPtr;
native WCharPtr;
native ConstWCharPtr;

/// Interoperable Object Request.
/// 
/// Interface to the implementation of the some Inter-ORB protocol.
/// NOTE: Marhsal/unmarshal operations on the client side may not cause the preemption.
pseudo interface IORequest
{
  ///@{
  /// Marshal/unmarshal data that meet the common data representation.

  /// Marshal CDR data.
  /// 
  /// \param align CDR data alignment.
  ///   For the constructed types is an alignment of the first primitive type.
  /// 
  /// \param size CDR data size.
  /// 
  /// \param data Pointer to the data with common-data-representation (CDR).
  void marshal (in Nirvana::Size align, in Nirvana::Size size, in Nirvana::ConstPointer data);

  /// Unmarshal CDR data.
  /// 
  /// \param align CDR data alignment.
  ///   For the constructed types is an alignment of the first primitive type.
  /// 
  /// \param size CDR data size.
  /// 
  /// \param data Pointer to the data buffer.
  /// 
  /// \returns `true` if the byte order must be swapped after unmarshaling.
  boolean unmarshal (in Nirvana::Size align, in Nirvana::Size size, in Nirvana::Pointer data);

  /// Marshal CDR sequence or array.
  /// 
  /// \param align CDR data alignment.
  ///   For the constructed types is an alignment of the first primitive type.
  /// 
  /// \param element_size Element size in memory.
  /// 
  /// \param CDR_element_size CDR element size.
  ///   CDR_element_size <= element_size.
  /// 
  /// \param element_count Count of elements.
  /// 
  /// \param data Pointer to the data with common-data-representation (CDR).
  /// 
  /// \param allocated_size If this parameter is not zero, the request may adopt the memory block.
  ///   When request adopts the memory block, it sets \p allocated_size to 0.
  /// 
  void marshal_seq (in Nirvana::Size align, in Nirvana::Size element_size,
    in Nirvana::Size CDR_element_size, in Nirvana::Size element_count, in Nirvana::Pointer data,
    inout Nirvana::Size allocated_size);

  /// Unmarshal CDR sequence or array.
  /// 
  /// \param align Data alignment.
  /// 
  /// \param element_size Element size in memory.
  /// 
  /// \param CDR_element_size CDR element size.
  ///   CDR_element_size <= element_size.
  /// 
  /// \param [out] element_count Count of elements.
  /// 
  /// \param [inout] data Pointer to the allocated memory block with common-data-representation (CDR).
  ///   The caller becomes an owner of this memory block.
  /// 
  /// \param [inout] allocated_size Size of the allocated memory block.
  ///              
  /// \returns `true` if the byte order must be swapped after unmarshaling.
  /// 
  boolean unmarshal_seq (in Nirvana::Size align, in Nirvana::Size element_size,
    in Nirvana::Size CDR_element_size, out Nirvana::Size element_count, inout Nirvana::Pointer data,
    inout Nirvana::Size allocated_size);

  ///@}

  ///@{
  /// Marshal/unmarshal sequences.

  /// Write marshaling sequence size.
  /// 
  /// \param element_count The sequence size.
  /// \returns `false` to skip marshaling.
  boolean marshal_seq_begin (in Nirvana::Size element_count);

  /// Get unmarshalling sequence size.
  /// 
  /// \returns The sequence size.
  Nirvana::Size unmarshal_seq_begin ();

  ///@}

  ///@{
  /// Marshal/unmarshal character data.

  void marshal_char (in Nirvana::Size count, in ConstCharPtr data);
  void unmarshal_char (in Nirvana::Size count, in CharPtr data);

  void marshal_string (inout string s, in boolean move);
  void unmarshal_string (inout string s);

  typedef sequence <char> CharSeq;
  void marshal_char_seq (inout CharSeq s, in boolean move);
  void unmarshal_char_seq (inout CharSeq s);

  void marshal_wchar (in Nirvana::Size count, in ConstWCharPtr data);
  void unmarshal_wchar (in Nirvana::Size count, in WCharPtr data);

  void marshal_wstring (inout wstring s, in boolean move);
  void unmarshal_wstring (inout wstring s);

  typedef sequence <wchar> WCharSeq;
  void marshal_wchar_seq (inout WCharSeq s, in boolean move);
  void unmarshal_wchar_seq (inout WCharSeq s);

  ///@}

  ///@{
  /// Object operations.

  /// Marshal interface.
  /// 
  /// \param itf The interface derived from Object or LocalObject.
  void marshal_interface (in _Interface itf);

  /// Unmarshal interface.
  /// 
  /// \param rep_id The interface repository id.
  /// 
  /// \returns Interface.
  _Interface unmarshal_interface (in string interface_id);

  /// Marshal TypeCode.
  /// 
  /// \param tc TypeCode.
  void marshal_type_code (in TypeCode tc);

  /// Unmarshal TypeCode.
  /// 
  /// \returns TypeCode.
  TypeCode unmarshal_type_code ();

  /// Marshal value type.
  /// 
  /// \param val    Value type interface.
  void marshal_value (in _Interface val);

  /// Unmarshal value type.
  /// 
  /// \param rep_id The value type repository id.
  /// 
  /// \returns Value type interface.
  _Interface unmarshal_value (in string interface_id);

  /// Marshal abstract interface.
  /// 
  /// \param itf The interface derived from AbstractBase.
  void marshal_abstract (in _Interface itf);

  /// Unmarshal abstract interface.
  /// 
  /// \param rep_id The interface repository id.
  /// 
  /// \returns Interface.
  _Interface unmarshal_abstract (in string interface_id);

  ///@}

  /// Response flags.
  readonly attribute unsigned short response_flags;

  /// Response is expected.
  const unsigned short RESPONSE_EXPECTED = 1;

  /// Response is expected with output data.
  /// If this flag is not set, a non exception reply should contain an empty body, i.e.,
  /// the equivalent of a void operation with no out/inout parameters.
  /// If this flag is set, RESPONSE_EXPECTED must be set also.
  const unsigned short RESPONSE_DATA = 2;

  ///@{
  /// Callee operations.

  /// End of data unmarshaling.
  /// 
  /// Called by proxy when all input or return data was successfully unmarshaled.
  /// Marshaling resources may be released.
  /// If not all data was unmarshalled, may throw the MARSHAL exception.
  void unmarshal_end ();

  /// Return exception to caller.
  /// Operation has move semantics so \p e may be cleared.
  void set_exception (inout any e);

  /// Marks request as successful.
  void success ();

  ///@}

  ///@{
  /// Caller operations.

  /// Invoke request.
  void invoke ();

  /// Unmarshal exception.
  /// \param [out] e If request completed with exception, receives the exception.
  ///                Otherwise unchanged.
  /// \returns `true` if e contains an exception.
  boolean get_exception (out any e);

  ///@}

  ///@{
  /// Asynchronous caller operations.

  /// Cancel the request.
  void cancel ();

  ///@}
};

};
};

#endif
